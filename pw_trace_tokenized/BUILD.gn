# Copyright 2020 The Pigweed Authors
#
# Licensed under the Apache License, Version 2.0 (the "License"); you may not
# use this file except in compliance with the License. You may obtain a copy of
# the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations under
# the License.

import("//build_overrides/pigweed.gni")

import("$dir_pw_build/facade.gni")
import("$dir_pw_build/target_types.gni")
import("$dir_pw_protobuf_compiler/proto.gni")
import("$dir_pw_thread/backend.gni")
import("$dir_pw_trace/backend.gni")
import("$dir_pw_unit_test/test.gni")
import("$pw_external_nanopb/nanopb.gni")
import("config.gni")
import("lock.gni")

pw_source_set("interrupt_spin_lock") {
  public_configs = [
    ":public_include_path",
    ":interrupt_spin_lock_public_overrides_include_path",
  ]
  public = [
    "interrupt_spin_lock_public_overrides/pw_trace_tokenized_backend/lock_native.h",
    "public/pw_trace_tokenized/internal/interrupt_spin_lock_native.h",
  ]
  public_deps = [ "$dir_pw_sync:interrupt_spin_lock" ]
}

config("interrupt_spin_lock_public_overrides_include_path") {
  include_dirs = [ "interrupt_spin_lock_public_overrides" ]
  visibility = [ ":*" ]
}

pw_source_set("no_lock") {
  public_configs = [
    ":public_include_path",
    ":no_lock_public_overrides_include_path",
  ]
  public = [
    "no_lock_public_overrides/pw_trace_tokenized_backend/lock_native.h",
    "public/pw_trace_tokenized/internal/no_lock_native.h",
  ]
  public_deps = [ "$dir_pw_sync:no_lock" ]
}

config("no_lock_public_overrides_include_path") {
  include_dirs = [ "no_lock_public_overrides" ]
  visibility = [ ":*" ]
}

config("public_include_path") {
  include_dirs = [ "public" ]
  visibility = [ ":*" ]
}

config("backend_config") {
  include_dirs = [ "public_overrides" ]
}

# Some tests in this file do not compile unless pw_trace_tokenized is the trace
# backend.
_pw_trace_tokenized_is_selected =
    get_label_info(pw_trace_BACKEND, "label_no_toolchain") ==
    get_label_info(":pw_trace_tokenized", "label_no_toolchain") &&
    pw_trace_tokenizer_time != ""

pw_source_set("config") {
  public_deps = [
    dir_pw_varint,
    pw_trace_CONFIG,
  ]
  public_configs = [ ":public_include_path" ]
  public = [ "public/pw_trace_tokenized/config.h" ]
}

config("linux_include_config_overrides") {
  cflags = [
    "-include",
    rebase_path("linux_config_overrides.h", root_build_dir),
  ]
  visibility = [ ":*" ]
}

pw_source_set("linux_config_overrides") {
  public_configs = [ ":linux_include_config_overrides" ]
  sources = [ "linux_config_overrides.h" ]
}

pw_test_group("tests") {
  tests = [
    ":trace_tokenized_test",
    ":tokenized_trace_buffer_test",
    ":tokenized_trace_buffer_log_test",
    ":trace_service_pwpb_test",
    ":decoder_test",
    ":transfer_handler_test",
  ]
}

pw_source_set("decoder") {
  public_configs = [ ":public_include_path" ]
  public = [ "public/pw_trace_tokenized/decoder.h" ]
  sources = [ "decoder.cc" ]
  public_deps = [
    ":pw_trace_tokenized",
    "$dir_pw_bytes",
    "$dir_pw_log",
    "$dir_pw_result",
    "$dir_pw_status",
    "$dir_pw_stream",
    "$dir_pw_string",
    "$dir_pw_third_party/fuchsia:stdcompat",
    "$dir_pw_tokenizer:decoder",
    "$dir_pw_varint:stream",
  ]
}

pw_test("decoder_test") {
  enable_if = _pw_trace_tokenized_is_selected
  deps = [
    ":decoder",
    "$dir_pw_assert:check",
    "$dir_pw_log",
    "$dir_pw_stream",
    "$dir_pw_tokenizer:decoder",
    "$dir_pw_trace",
  ]
  sources = [ "decoder_test.cc" ]
}

pw_source_set("pw_trace_tokenized") {
  public_configs = [
    ":backend_config",
    ":public_include_path",
  ]
  public_deps = [
    ":config",
    ":core",
    "$dir_pw_tokenizer",
  ]
  deps = []
  if (pw_trace_tokenizer_time != "") {
    deps += [ pw_trace_tokenizer_time ]
  }
  public = [ "public_overrides/pw_trace_backend/trace_backend.h" ]
}

pw_test("trace_tokenized_test") {
  enable_if = _pw_trace_tokenized_is_selected
  deps = [
    ":core",
    "$dir_pw_thread:sleep",
    "$dir_pw_trace",
  ]

  sources = [ "trace_test.cc" ]
}

config("trace_buffer_size") {
  defines = [ "PW_TRACE_BUFFER_SIZE_BYTES=${pw_trace_tokenized_BUFFER_SIZE}" ]
}

pw_proto_library("protos") {
  sources = [
    "pw_trace_protos/trace.proto",
    "pw_trace_protos/trace_rpc.proto",
    "pw_trace_protos/trace_service.proto",
  ]
  inputs = [
    "pw_trace_protos/trace_rpc.options",
    "pw_trace_protos/trace_service.options",
  ]
  python_package = "py"
  deps = [ "$dir_pw_chrono:protos" ]
}

pw_source_set("trace_rpc_service") {
  public_configs = [ ":public_include_path" ]
  public = [ "public/pw_trace_tokenized/trace_rpc_service_nanopb.h" ]
  public_deps = [ ":protos.nanopb_rpc" ]
  deps = [
    ":core",
    ":tokenized_trace_buffer",
    "$dir_pw_log",
  ]
  sources = [ "trace_rpc_service_nanopb.cc" ]
}

pw_source_set("base_trace_service") {
  public_configs = [ ":public_include_path" ]
  public_deps = [
    ":core",
    dir_pw_status,
    dir_pw_stream,
  ]
  deps = [
    ":tokenized_trace_buffer",
    dir_pw_log,
  ]
  sources = [
    "base_trace_service.cc",
    "public/pw_trace_tokenized/base_trace_service.h",
  ]
}

pw_source_set("trace_service_pwpb") {
  public_configs = [ ":public_include_path" ]
  public_deps = [
    ":base_trace_service",
    ":protos.pwpb_rpc",
  ]
  sources = [
    "public/pw_trace_tokenized/trace_service_pwpb.h",
    "trace_service_pwpb.cc",
  ]
  deps = []
  if (pw_trace_tokenizer_time != "") {
    deps += [ pw_trace_tokenizer_time ]
  }
}

pw_test("trace_service_pwpb_test") {
  enable_if = _pw_trace_tokenized_is_selected
  deps = [
    ":trace_service_pwpb",
    "$dir_pw_chrono:system_clock",
    "$dir_pw_rpc/pwpb:test_method_context",
    "$dir_pw_trace",
  ]
  sources = [ "trace_service_pwpb_test.cc" ]
}

pw_source_set("tokenized_trace_buffer") {
  public_configs = [
    ":public_include_path",
    ":trace_buffer_size",
  ]
  public = [ "public/pw_trace_tokenized/trace_buffer.h" ]
  public_deps = [
    ":config",
    "$dir_pw_containers:algorithm",
    "$dir_pw_containers:inline_var_len_entry_queue",
    dir_pw_bytes,
    dir_pw_status,
  ]
  sources = [ "trace_buffer.cc" ]
  deps = [
    ":core",
    dir_pw_span,
  ]
}

pw_test("tokenized_trace_buffer_test") {
  enable_if = _pw_trace_tokenized_is_selected
  sources = [ "trace_buffer_test.cc" ]
  deps = [
    ":tokenized_trace_buffer",
    dir_pw_trace,
  ]
}

pw_source_set("tokenized_trace_buffer_log") {
  public_configs = [ ":public_include_path" ]
  public = [ "public/pw_trace_tokenized/trace_buffer_log.h" ]
  public_deps = [ dir_pw_status ]
  deps = [
    ":config",
    ":tokenized_trace_buffer",
    dir_pw_base64,
    dir_pw_log,
    dir_pw_string,
    dir_pw_trace,
  ]
  sources = [ "trace_buffer_log.cc" ]
}

pw_test("tokenized_trace_buffer_log_test") {
  enable_if = _pw_trace_tokenized_is_selected
  deps = [
    ":core",
    ":tokenized_trace_buffer_log",
    dir_pw_trace,
  ]
  sources = [ "trace_buffer_log_test.cc" ]
}

pw_source_set("fake_trace_time") {
  deps = [ ":config" ]
  sources = [ "fake_trace_time.cc" ]
}

pw_source_set("host_trace_time") {
  deps = [ ":config" ]
  sources = [ "host_trace_time.cc" ]
}

pw_facade("lock") {
  backend = pw_trace_tokenized_LOCK_BACKEND
  public_configs = [ ":public_include_path" ]
  public = [ "public/pw_trace_tokenized/internal/lock.h" ]
}

pw_source_set("core") {
  public_configs = [
    ":backend_config",
    ":public_include_path",
  ]
  public = [
    "public/pw_trace_tokenized/internal/trace_tokenized_internal.h",
    "public/pw_trace_tokenized/trace_callback.h",
    "public/pw_trace_tokenized/trace_tokenized.h",
  ]
  public_deps = [
    ":config",
    dir_pw_preprocessor,
    dir_pw_span,
    dir_pw_status,
    dir_pw_tokenizer,
  ]
  sources = [ "trace.cc" ]
  deps = [
    ":lock",
    "$dir_pw_trace:facade",
    dir_pw_varint,
  ]
  if (pw_trace_tokenizer_time != "") {
    deps += [ pw_trace_tokenizer_time ]
  }
  visibility = [ ":*" ]
}

pw_source_set("transfer_handler") {
  public_configs = [ ":public_include_path" ]
  public_deps = [
    ":config",
    dir_pw_transfer,
  ]
  deps = [
    ":tokenized_trace_buffer",
    "$dir_pw_containers:inline_var_len_entry_queue",
    "$dir_pw_containers:var_len_entry_queue",
    dir_pw_log,
  ]
  sources = [ "transfer_handler.cc" ]
  public = [ "public/pw_trace_tokenized/transfer_handler.h" ]
  visibility = [ ":*" ]
}

config("trace_example_config") {
  include_dirs = [ "example/public" ]
}

pw_test("transfer_handler_test") {
  enable_if = _pw_trace_tokenized_is_selected
  deps = [
    ":decoder",
    ":pw_trace_tokenized",
    ":tokenized_trace_buffer",
    ":transfer_handler",
    "$dir_pw_string:builder",
    "$dir_pw_tokenizer:decoder",
    dir_pw_trace,
  ]
  sources = [ "transfer_handler_test.cc" ]
}

pw_source_set("trace_example_to_file") {
  public_deps = [ ":pw_trace_tokenized" ]
  public_configs = [ ":trace_example_config" ]
  public = [ "example/public/pw_trace_tokenized/example/trace_to_file.h" ]
}

# Builds trace examples
pw_executable("trace_tokenized_example_basic") {
  deps = [
    ":trace_example_to_file",
    "$dir_pw_log",
    "$dir_pw_trace",
    "$dir_pw_trace:trace_sample_app",
  ]
  sources = [ "example/basic.cc" ]
}

pw_executable("trace_tokenized_example_trigger") {
  deps = [
    ":trace_example_to_file",
    "$dir_pw_log",
    "$dir_pw_trace",
    "$dir_pw_trace:trace_sample_app",
  ]
  sources = [ "example/trigger.cc" ]
}

pw_executable("trace_tokenized_example_filter") {
  deps = [
    ":trace_example_to_file",
    "$dir_pw_log",
    "$dir_pw_trace",
    "$dir_pw_trace:trace_sample_app",
  ]
  sources = [ "example/filter.cc" ]
}

if (dir_pw_third_party_nanopb == "") {
  group("trace_tokenized_example_rpc") {
  }
} else {
  pw_executable("trace_tokenized_example_rpc") {
    sources = [ "example/rpc.cc" ]
    deps = [
      ":pw_trace_tokenized",
      ":trace_rpc_service",
      "$dir_pw_assert:check",
      "$dir_pw_rpc:server",
      "$dir_pw_rpc/system_server",
      "$dir_pw_trace:trace_sample_app",
      dir_pw_hdlc,
      dir_pw_log,
      dir_pw_trace,
    ]
  }
}

if (current_os != "linux") {
  group("trace_tokenized_example_linux_group_by_tid") {
  }
} else {
  pw_executable("trace_tokenized_example_linux_group_by_tid") {
    sources = [ "example/linux_group_by_tid.cc" ]
    deps = [
      ":trace_example_to_file",
      "$dir_pw_log",
      "$dir_pw_trace",
    ]
  }
}
